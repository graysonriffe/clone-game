# Actor class - Base class of Player and Clone
@abstract
class_name Actor
extends CharacterBody3D

const SPEED = 5.0
const JUMP_VELOCITY = 4.5

var movementDirectionSmoothed: Vector3

@onready var head: Node3D = $Head

func _physics_process(delta: float) -> void:
    # Add the gravity.
    if not is_on_floor():
        velocity += get_gravity() * delta
    
    # Get direction vector from either the Player or Clone
    var inputDirection: Vector2 = _getInputDirection()
    var direction: Vector3 = (transform.basis * Vector3(inputDirection.x, 0, inputDirection.y)).normalized()
    
    # Smooth the movement, and differently depending on if the actor is mid-air
    if is_on_floor():
        movementDirectionSmoothed = lerp(movementDirectionSmoothed, direction, 10.0 * delta)
    elif inputDirection != Vector2.ZERO:
        movementDirectionSmoothed = lerp(movementDirectionSmoothed, direction, 3.0 * delta)
    
    # Apply the movement
    velocity.x = movementDirectionSmoothed.x * SPEED
    velocity.z = movementDirectionSmoothed.z * SPEED
    
    move_and_slide()


@abstract
func _getInputDirection() -> Vector2


func jump():
    velocity.y = JUMP_VELOCITY
